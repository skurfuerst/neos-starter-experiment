prototype(Neos.Demo:Document.Fragment.Menu.Main) < prototype(Neos.Fusion:Component) {
    root = ${site}
    menuItems = Neos.Neos:MenuItems

    renderer = afx`
        <nav class="navbar navbar-static-top navbar-default first-level-navigation" role="navigation">
            <div class="navbar-header">
                <button type="button" class="navbar-toggle" data-toggle="collapse" data-target=".navbar-main-collapse">
                    <span class="sr-only">Toggle navigation</span>
                    <span class="icon-bar"></span>
                    <span class="icon-bar"></span>
                    <span class="icon-bar"></span>
                </button>

                <Neos.Neos:NodeLink node={props.root}>
                    <svg
                        version="1.1"
                        class="navbar-brandlogo"
                        xmlns="http://www.w3.org/2000/svg"
                        xmlns:xlink="http://www.w3.org/1999/xlink" x="0px" y="0px"
                        width="200px" height="200px" viewBox="0 0 200 200"
                        enable-background="new 0 0 200 200" xml:space="preserve"
                    >
                        <g>
                            <polygon fill="#26224C" points="132.984,37.5 112.342,52.662 112.342,84.378 132.984,113.791 	"/>
                            <polygon fill="#26224C" points="132.984,150.564 53.627,37.5 44.434,44.273 44.434,162.5 65.076,147.338 65.076,88.79
                            116.695,162.5 139.275,162.5 155.566,150.564 	"/>
                            <polygon fill="#00ADEE" points="65.076,88.79 65.076,147.338 44.434,162.5 67.016,162.5 87.658,147.338 87.658,121.038 	"/>
                            <polygon fill="#00ADEE" points="132.984,113.791 132.984,37.5 155.566,37.5 155.566,150.564 132.984,150.564 53.627,37.5
                            79.436,37.5 	"/>
                        </g>
                    </svg>
                </Neos.Neos:NodeLink>
            </div>

            <div class="collapse navbar-collapse navbar-main-collapse">
                <ul class="nav nav-pills nav-justified">
                    <Neos.Fusion:Loop items={props.menuItems}>
                        <li class={item.state}>
                            <Neos.Neos:NodeLink node={item.node}>{item.label}</Neos.Neos:NodeLink>
                            <ul
                                @if.has={item.subItems && (item.state != 'normal')}
                                class="second-level-sub-navigation nav nav-justified visible-xs"
                            >
                                <Neos.Fusion:Loop items={item.subItems} itemName="item">
                                    <li class={item.state}>
                                        <Neos.Neos:NodeLink node={item.node}>{item.label}</Neos.Neos:NodeLink>
                                    </li>
                                </Neos.Fusion:Loop>
                            </ul>
                        </li>
                    </Neos.Fusion:Loop>
                </ul>
            </div>
        </nav>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${documentNode}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
}
